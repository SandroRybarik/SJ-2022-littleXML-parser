# Finálny tvar gramatiky

- Všetký terminály sú malým písmom. (letter, digit, ...)
- Všetký neterminály sú veľkým písmom. (S, XMLDECL, ...)

## Úpravy:

Upravili sme hlavne pôvodné pravidlo
`element -> emptyelementtag | starttag words endtag | starttag elements endtag`
kde sme sa zbavili _emptyelementtag_ a vytvorili sme pravidlá:

```
ELEMENT        -> langle NAME ELEMENT2
ELEMENT2       -> slash_rangle
ELEMENT2       -> rangle ELEMENT3
ELEMENT3       -> WORDS ENDTAG
ELEMENT3       -> ELEMENTS ENDTAG
ENDTAG         -> langle_slash NAME rangle
```

ktoré riešia prípady ako: `<a/>`, `<a></a>`, `<a>x</a>`, `<a><b></b></a>`.

---

Ďalej sme upravili pravidlo `words -> {word}.`, tak aby sme zohľadnili aj medzery. V pôvodnom návrhu je, že slová môžu byť aj prázdne, ale upravili sme to na aspoň jedno slovo z dôvodu, že prázdne slovo v kontexte gramatiky rieši `ELEMENTS -> ε`, kde vznikol konflikt, kedy sa nedalo rozoznať `<a></a>`, či medzi tagmi prazdne slovo alebo prázdny element.

V pôvodnej gramatike to zohľadnené nie je, takže sme pridali pravidlá:

```
WORDS          -> WORD WORDS2
WORDS2         -> space WORDS
WORDS2         -> ε
```

a neterminál _space_.

---

Dalšia úprava pravidla `name := (letter | '_' | ':' ) {namechar}.`, kde sme zabezpečili, že názov musí začínať buď _znakom_, ___ alebo _:_ a ďalším ľubovoľným počtom _NAMECHAR_. Pravidlá:

```
NAME           -> LETTER NAMECHAR
NAME           -> underscore NAMECHAR
NAME           -> colon NAMECHAR
NAMECHAR       -> LETTER NAMECHAR
NAMECHAR       -> DIGIT NAMECHAR
NAMECHAR       -> dot NAMECHAR
NAMECHAR       -> minus NAMECHAR
NAMECHAR       -> underscore NAMECHAR
NAMECHAR       -> colon NAMECHAR
NAMECHAR       -> ε
```

---

Ostatné úpravy boli jednoduché, kde nevznikali problémy a bol to priamy prepis pravidiel.

## Finálny tvar gramatiky

```
S              -> XMLDECL ELEMENT
S              -> ELEMENT
XMLDECL        -> xml_open xml_ver VERNUMB xml_close
VERNUMB        -> NUMBER dot NUMBER
ELEMENT        -> langle NAME ELEMENT2
ELEMENT2       -> slash_rangle
ELEMENT2       -> rangle ELEMENT3
ELEMENT3       -> WORDS ENDTAG
ELEMENT3       -> ELEMENTS ENDTAG
ENDTAG         -> langle_slash NAME rangle
WORDS          -> WORD WORDS2
WORDS2         -> space WORDS
WORDS2         -> ε
ELEMENTS       -> ELEMENT
ELEMENTS       -> ε
NAME           -> LETTER NAMECHAR
NAME           -> underscore NAMECHAR
NAME           -> colon NAMECHAR
NAMECHAR       -> LETTER NAMECHAR
NAMECHAR       -> DIGIT NAMECHAR
NAMECHAR       -> dot NAMECHAR
NAMECHAR       -> minus NAMECHAR
NAMECHAR       -> underscore NAMECHAR
NAMECHAR       -> colon NAMECHAR
NAMECHAR       -> ε
LETTER         -> letter
NUMBER         -> DIGIT NUMBER2
NUMBER2        -> DIGIT NUMBER2
NUMBER2        -> ε
DIGIT          -> digit
WORD           -> CHAR WORD2
WORD2          -> CHAR WORD2
WORD2          -> ε
CHAR           -> LETTER
CHAR           -> DIGIT
CHAR           -> question_mark
CHAR           -> exclamation_mark
CHAR           -> curly_left
CHAR           -> curly_right
```
